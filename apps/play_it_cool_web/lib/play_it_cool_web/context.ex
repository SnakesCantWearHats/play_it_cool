defmodule PlayItCoolWeb.Context do
  @behaviour Plug

  import Plug.Conn

  def init(opts), do: opts

  def call(conn, _) do
    context = build_context(conn)
    Absinthe.Plug.put_options(conn, context: context)
  end

  @doc """
  Return the current user context based on the authorization header
  """
  def build_context(conn) do
    with ["Bearer " <> token] <- get_req_header(conn, "authorization"),
         {:ok, current_user} <- authorize(token) do
      %{current_user: current_user}
    else
      _ -> %{}
    end
  end

  defp authorize(token) do
    case Phoenix.Token.verify(PlayItCoolWeb.Endpoint, "user auth", token) do
      {:error, _} ->
        {:error, "Invalid authorization token"}

      {:ok, data} ->
        {:ok, data}
    end
  end
end
